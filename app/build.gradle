apply plugin: "com.android.application"
apply from: rootProject.file("gradle/base-module.gradle")

android {

    namespace 'co.adrianblan.cheddar'

    defaultConfig {
        applicationId = "co.adrianblan.cheddar"
        versionCode = 1
        versionName = "0.1"
    }

    signingConfigs {
        getByName("debug") {
            storeFile = rootProject.file("debug.keystore")
            storePassword = "debugkey"
            keyAlias = "debugkey"
            keyPassword = "debugkey"
        }
        create("release") {
            def propsFile = rootProject.file("keystore.properties")
            if (propsFile.exists()) {
                def props = Properties()
                props.load(FileInputStream(propsFile))
                storeFile = file(props["storeFile"])
                storePassword = props["storePassword"]
                keyAlias = props["keyAlias"]
                keyPassword = props["keyPassword"]
            } else {
                println("File keystore.properties not found.")
            }
        }
    }

    buildTypes {
        getByName("debug") {
            signingConfig = signingConfigs.getByName("debug")
            applicationIdSuffix ".debug"
            minifyEnabled false
        }
        getByName("release") {
            signingConfig = signingConfigs.getByName("release")
            minifyEnabled = true
            shrinkResources = true
            proguardFiles(
                    getDefaultProguardFile("proguard-android-optimize.txt"),
                    "../proguard-rules.pro"
            )
        }
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/AL2.0', 'META-INF/LGPL2.1']
        }
    }

    buildFeatures {
        compose true
        buildConfig true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.androidxComposeCompiler.get()
    }
}

dependencies {
    implementation(project(":feature-storynavigation"))
    implementation(project(":feature-storyfeed"))
    implementation(project(":feature-storydetail"))

    implementation(project(":core"))
    implementation(project(":common"))
    implementation(project(":common-ui"))
    implementation(project(":common-test"))
    implementation(project(":network"))

    debugImplementation(libs.leakcanary)
}